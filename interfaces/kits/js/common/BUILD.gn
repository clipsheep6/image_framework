# Copyright (C) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//build/ohos/ace/ace.gni")
import("//foundation/graphic/graphic_2d/graphic_config.gni")
import("$image_subsystem/ide/image_decode_config.gni")

config("image_external_config") {
  include_dirs = [
    "include",
    "//utils/system/safwk/native/include",
    "${image_subsystem}/frameworks/kits/js/common/ndk/include",
    "//foundation/ability/ability_runtime/interfaces/inner_api/runtime/include/",
    "$image_subsystem/frameworks/innerkitsimpl/receiver/include",
    "$image_subsystem/frameworks/innerkitsimpl/creator/include",
    "$image_subsystem/frameworks/innerkitsimpl/pixelconverter/include",
    "$image_subsystem/frameworks/innerkitsimpl/converter/include",
    "$image_subsystem/frameworks/innerkitsimpl/codec/include",
    "$image_subsystem/frameworks/innerkitsimpl/common/include",
    "$image_subsystem/frameworks/innerkitsimpl/stream/include",
    "$image_subsystem/frameworks/innerkitsimpl/utils/include",
    "$image_subsystem/frameworks/kits/js/common/include",
    "$image_subsystem/interfaces/kits/js/common/include",
    "//foundation/multimedia/utils/include",
    "$image_subsystem/plugins/common/libs/image/libjpegplugin",
    "$image_subsystem/plugins/manager/include",
    "$image_subsystem/plugins/manager/include/image",
    "$image_subsystem/interfaces/innerkits/include",
    "$image_subsystem/interfaces/kits/native/include",
    "//utils/jni/jnikit/include",
    "//base/hiviewdfx/hilog/interfaces/native/innerkits/include",
    "//foundation/graphic/graphic_2d/interfaces/inner_api/surface",
    "//foundation/graphic/graphic_2d/interfaces/inner_api/common",
    "//foundation/graphic/graphic_2d/interfaces/kits/napi/graphic/color_manager/color_space_object_convertor",
    "//foundation/communication/ipc/interfaces/innerkits/ipc_core/include",
    "//foundation/graphic/graphic_2d/utils/buffer_handle/export",
    "//foundation/graphic/graphic_2d/utils/color_manager/export",
    "//drivers/peripheral/display/interfaces/include",
    "//drivers/peripheral/base",
    "$skia_root/skia/third_party/externals/libjpeg-turbo",
    "$ipc_subsystem/communication/ipc/ipc/native/src/napi_common/include",
  ]

  include_dirs += [
    "$image_subsystem/plugins/manager/include/pluginbase",
    "$image_subsystem/plugins/common/libs/image/libjpegplugin/include",
    "$image_subsystem/frameworks/innerkitsimpl/utils/include",
    "$image_subsystem/frameworks/innerkitsimpl/converter/include",
    "$skia_root/skia/include/codec",
    "$skia_root/skia",
    "$skia_root/skia/include/core",
    "$skia_root/skia/third_party/libjpeg-turbo",
    "$skia_root/skia/third_party/skcms",
    "//third_party/libexif",
    "//third_party/zlib",
  ]

  if (use_mingw_win) {
    include_dirs +=
        [ "$image_subsystem/mock/native/include" ]
  } else if (use_clang_mac) {
    include_dirs += [
      "$image_subsystem/mock/native/include",
      "//third_party/bounds_checking_function/include",
      "//commonlibrary/c_utils/base/include",
      "//base/hiviewdfx/hilog/interfaces/native/innerkits/include",
    ]
  } else if (use_clang_ios) {
    include_dirs += [
      "//third_party/libpng",
      "$image_subsystem/mock/native/include/log",
      "$image_subsystem/mock/native/include/secure",
    ]
    include_dirs -= [
      "$image_subsystem/frameworks/innerkitsimpl/receiver/include",
      "$image_subsystem/frameworks/innerkitsimpl/creator/include",
      "//foundation/graphic/graphic_2d/interfaces/inner_api/surface",
      "//foundation/graphic/graphic_2d/interfaces/inner_api/common",
      "//foundation/graphic/graphic_2d/utils/buffer_handle/export",
      "//foundation/graphic/graphic_2d/utils/color_manager/export",
    ]
  } else if (use_clang_android) {
    include_dirs += [
      "//commonlibrary/c_utils/base/include",
      "//third_party/libpng",
      "$image_subsystem/mock/native/include/log",
    ]

    include_dirs -= [
      "$image_subsystem/frameworks/innerkitsimpl/receiver/include",
      "$image_subsystem/frameworks/innerkitsimpl/creator/include",
      "//foundation/graphic/graphic_2d/interfaces/inner_api/surface",
      "//foundation/graphic/graphic_2d/interfaces/inner_api/common",
      "//foundation/graphic/graphic_2d/utils/buffer_handle/export",
      "//foundation/graphic/graphic_2d/utils/color_manager/export",
    ]
  } else {
    include_dirs += [
      "//commonlibrary/c_utils/base/include",
      "//third_party/libpng",
      "//base/hiviewdfx/hilog/interfaces/native/innerkits/include",
    ]
  }
}

js_declaration("image_js") {
  part_name = "image_framework"
  sources = [ "$image_subsystem/interfaces/kits/js/@ohos.multimedia.image.d.ts" ]
}

ohos_copy("image_declaration") {
  sources = [ "$image_subsystem/interfaces/kits/js/@ohos.multimedia.image.d.ts" ]
  outputs = [ target_out_dir + "/$target_name/" ]
  module_source_dir = target_out_dir + "/$target_name"
  module_install_name = ""
}

if (use_clang_ios) {
  ohos_source_set("image") {
    public_configs = [ ":image_external_config" ]
    sources = [
      "$image_subsystem/frameworks/kits/js/common/image_pixel_map_napi_kits.cpp",
      "$image_subsystem/frameworks/kits/js/common/image_napi_utils.cpp",
      "$image_subsystem/frameworks/kits/js/common/image_packer_napi.cpp",
      "$image_subsystem/frameworks/kits/js/common/image_source_napi.cpp",
      "$image_subsystem/frameworks/kits/js/common/native_module_ohos_image.cpp",
      "$image_subsystem/frameworks/kits/js/common/pixel_map_napi.cpp",
    ]

    cflags = [ "-DIMAGE_DEBUG_FLAG" ]

    defines = image_decode_ios_defines
    deps = [
      "//foundation/arkui/napi:ace_napi",
      "$image_subsystem/frameworks/innerkitsimpl/pixelconverter:pixelconvertadapter",
      "$image_subsystem/frameworks/innerkitsimpl/utils:image_utils",
      "$image_subsystem/interfaces/innerkits:image_native",
      "$image_subsystem/mock/native:log_mock_static",
      "$image_subsystem/plugins/manager:pluginmanager",
    ]

    #relative_install_dir = "module/multimedia"
    subsystem_name = "graphic"
    part_name = "image_framework"
  }
} else {
  ohos_shared_library("image_napi") {
    if (!use_clang_android && !use_clang_ios) {
      sanitize = {
        cfi = true
        cfi_cross_dso = true
        debug = false
      }
    }
    sources = [
      "$image_subsystem/frameworks/kits/js/common/native_module_ohos_image.cpp",
    ]

    cflags = [
      "-DIMAGE_DEBUG_FLAG",
      "-DIMAGE_COLORSPACE_FLAG",
    ]

    deps = [
      ":image",
      "$image_subsystem/frameworks/innerkitsimpl/pixelconverter:pixelconvertadapter",
    ]

    external_deps = [ "napi:ace_napi" ]

    output_name = "libimage"
    relative_install_dir = "module/multimedia"
    subsystem_name = "graphic"
    part_name = "image_framework"
  }

  ohos_shared_library("image") {
    if (!use_clang_android && !use_clang_ios) {
      sanitize = {
        cfi = true
        cfi_cross_dso = true
        debug = false
      }
    }
    public_configs = [ ":image_external_config" ]
    sources = [
      "$image_subsystem/frameworks/kits/js/common/image_mdk_kits.cpp",
      "$image_subsystem/frameworks/kits/js/common/image_pixel_map_napi_kits.cpp",
      "$image_subsystem/frameworks/kits/js/common/image_receiver_mdk_kits.cpp",
      "$image_subsystem/frameworks/kits/js/common/image_source_mdk_kits.cpp",
      "$image_subsystem/frameworks/kits/js/common/image_creator_napi.cpp",
      "$image_subsystem/frameworks/kits/js/common/image_napi.cpp",
      "$image_subsystem/frameworks/kits/js/common/image_napi_utils.cpp",
      "$image_subsystem/frameworks/kits/js/common/image_packer_napi.cpp",
      "$image_subsystem/frameworks/kits/js/common/image_receiver_napi.cpp",
      "$image_subsystem/frameworks/kits/js/common/image_source_napi.cpp",
      "$image_subsystem/frameworks/kits/js/common/pixel_map_napi.cpp",
    ]

    cflags = [
      "-DIMAGE_DEBUG_FLAG",
      "-DIMAGE_COLORSPACE_FLAG",
    ]

    if (use_clang_android) {
      defines = image_decode_android_defines
      cflags -= [ "-DIMAGE_COLORSPACE_FLAG" ]
      sources -= [
        "$image_subsystem/frameworks/kits/js/common/image_mdk_kits.cpp",
        "$image_subsystem/frameworks/kits/js/common/image_receiver_mdk_kits.cpp",
        "$image_subsystem/frameworks/kits/js/common/image_creator_napi.cpp",
        "$image_subsystem/frameworks/kits/js/common/image_napi.cpp",
        "$image_subsystem/frameworks/kits/js/common/image_receiver_napi.cpp",
      ]
      deps = [
        "//commonlibrary/c_utils/base:utils",
        "//foundation/arkui/napi:ace_napi",
        "$image_subsystem/frameworks/innerkitsimpl/pixelconverter:pixelconvertadapter",
        "$image_subsystem/frameworks/innerkitsimpl/utils:image_utils",
        "$image_subsystem/interfaces/innerkits:image_native",
        "$image_subsystem/mock/native:log_mock_static",
        "$image_subsystem/plugins/manager:pluginmanager",
      ]
    } else {
      deps = [
        "$ipc_subsystem/communication/ipc/interfaces/innerkits/ipc_napi_common:ipc_napi",
        "//foundation/arkui/napi:ace_napi",
        "//foundation/graphic/graphic_2d:libsurface",
        "//foundation/graphic/graphic_2d/interfaces/kits/napi/graphic/color_manager:color_space_object_convertor",
        "//foundation/graphic/graphic_2d/utils/color_manager:color_manager",
        "$image_subsystem/frameworks/innerkitsimpl/pixelconverter:pixelconvertadapter",
        "$image_subsystem/interfaces/innerkits:image_native",
      ]
    }

    external_deps = [
      "c_utils:utils",
      "hichecker:libhichecker",
      "hitrace:hitrace_meter",
    ]
    output_name = "libimage_napi"
    subsystem_name = "graphic"
    innerapi_tags = [ "platformsdk" ]
    part_name = "image_framework"
  }

  if (use_clang_android) {
    ohos_shared_library("multimedia_image") {
      public_configs = [ ":image_external_config" ]
      sources = [
        "$image_subsystem/frameworks/kits/js/common/image_packer_napi.cpp",
        "$image_subsystem/frameworks/kits/js/common/image_source_napi.cpp",
        "$image_subsystem/frameworks/kits/js/common/native_module_ohos_image.cpp",
        "$image_subsystem/frameworks/kits/js/common/pixel_map_napi.cpp",
        "$image_subsystem/kits/js/common/image_napi_utils.cpp",
      ]

      cflags = [ "-DIMAGE_DEBUG_FLAG" ]

      defines = image_decode_android_defines
      deps = [
        "$commonlibrary_subsystem/base:utils",
        "$image_subsystem/frameworks/innerkitsimpl/pixelconverter:pixelconvertadapter",
        "$image_subsystem/frameworks/innerkitsimpl/utils:image_utils",
        "$image_subsystem/interfaces/innerkits:image_native",
        "$image_subsystem/mock/native:log_mock_static",
        "$image_subsystem/plugins/manager:pluginmanager",
        "$plugins_lib_root/napi:napi_android",
      ]

      external_deps = [
        "c_utils:utils",
        "hichecker:libhichecker",
        "hitrace:hitrace_meter",
      ]
      relative_install_dir = "module/multimedia"
      subsystem_name = "graphic"
      part_name = "image_framework"
    }
  }
}
