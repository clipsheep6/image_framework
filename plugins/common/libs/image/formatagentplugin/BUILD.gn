# Copyright (C) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/graphic/image_framework/ide/image_decode_config.gni")

ohos_shared_library("imageformatagent") {
  if (!use_clang_android && !use_clang_ios) {
    sanitize = {
      cfi = true
      cfi_cross_dso = true
      debug = false
    }
  }
  sources = [
    "$image_subsystem/plugins/common/libs/image/formatagentplugin/src/bmp_format_agent.cpp",
    "$image_subsystem/plugins/common/libs/image/formatagentplugin/src/gif_format_agent.cpp",
    "$image_subsystem/plugins/common/libs/image/formatagentplugin/src/heif_format_agent.cpp",
    "$image_subsystem/plugins/common/libs/image/formatagentplugin/src/jpeg_format_agent.cpp",
    "$image_subsystem/plugins/common/libs/image/formatagentplugin/src/plugin_export.cpp",
    "$image_subsystem/plugins/common/libs/image/formatagentplugin/src/png_format_agent.cpp",
    "$image_subsystem/plugins/common/libs/image/formatagentplugin/src/raw_format_agent.cpp",
    "$image_subsystem/plugins/common/libs/image/formatagentplugin/src/wbmp_format_agent.cpp",
    "$image_subsystem/plugins/common/libs/image/formatagentplugin/src/webp_format_agent.cpp",
    "src/svg_format_agent.cpp",
  ]

  include_dirs = [
    "//foundation/multimedia/utils/include",
    "$image_subsystem/plugins/manager/include",
    "$image_subsystem/plugins/manager/include/image",
    "$image_subsystem/plugins/manager/include/pluginbase",
    "$image_subsystem/plugins/common/libs/image/formatagentplugin/include",
    "$image_subsystem/plugins/manager/include/utils",
    "$image_subsystem/interfaces/innerkits/include",
  ]

  if (use_mingw_win) {
    defines = image_decode_windows_defines
    include_dirs +=
        [ "$image_subsystem/mock/native/include" ]
    deps = [
      "$image_subsystem/mock/native:log_mock_static",
      "$image_subsystem/plugins/manager:pluginmanager_static",
    ]
  } else if (use_clang_mac) {
    defines = image_decode_mac_defines
    include_dirs +=
        [ "$image_subsystem/mock/native/include" ]
    deps = [
      "$image_subsystem/mock/native:log_mock_static",
      "$image_subsystem/plugins/manager:pluginmanager_static",
    ]
  } else {
    include_dirs += [ "//commonlibrary/c_utils/base/include" ]
    deps = [
      "$image_subsystem/plugins/manager:pluginmanager",
    ]

    #    external_deps = [ "hilog:libhilog" ]
    external_deps = [
      "c_utils:utils",
      "hilog:libhilog",
    ]
  }

  part_name = "image_framework"

  subsystem_name = "graphic"
}

ohos_prebuilt_etc("formatagentmetadata") {
  source = "imageformatagent.pluginmeta"
  relative_install_dir = "multimediaplugin/image"
  subsystem_name = "graphic"
  part_name = "image_framework"
}
